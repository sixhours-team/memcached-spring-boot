import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE' apply false
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.jfrog.artifactory' version '4.15.2'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.sonarqube' version '3.0'
}

ext {
    springCloudVersion = 'Hoxton.SR4'
    xmemcachedVersion = '2.4.6'
    elasticacheClientVersion = '1.1.2'
    appengineApiVersion = '1.9.80'
    commonsLoggingVersion = '1.2'
    testcontainersVersion = '1.15.0-rc2'
}

ext.JAVA_GRADLE = "$rootDir/gradle/java.gradle"
ext.LICENSE_GRADLE = "$rootDir/gradle/license.gradle"
ext.PUBLISH_GRADLE = "$rootDir/gradle/publish.gradle"
ext.RELEASE_GRADLE = "$rootDir/gradle/release.gradle"
ext.SONAR_GRADLE = "$rootDir/gradle/sonar.gradle"

ext {
    projectVersion = '2.3.1-SNAPSHOT'
    useLastTag = false
}

allprojects {
    def gitVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            ignoreExitValue true
            commandLine 'git', 'describe', '--tags', '--abbrev=0', '--match', 'v[[:digit:]]*\\.[[:digit:]]*\\.[[:digit:]]*'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        return !version.isEmpty() ? version - 'v' : projectVersion
    }

    group = "io.sixhours"
    version = useLastTag.toBoolean() ? gitVersion() : projectVersion
}

subprojects {
    apply from: JAVA_GRADLE
    apply from: LICENSE_GRADLE
    apply from: PUBLISH_GRADLE
    apply from: RELEASE_GRADLE
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    jar {
        manifest {
            attributes('Implementation-Title': project.name)
            attributes('Implementation-Version': project.version)
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}
